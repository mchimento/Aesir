IMPORTS {
import main.ConPurse;
import main.Messages;
import main.SWResponses;
}

MODEL {

VARIABLES
{
ConPurse pfrom;
ConPurse pto;
short pvalue;
}

TRIGGERS {
start_from_operation (Messages m_sf, short ret) = {ConPurse sender.start_from_operation(m_sf)exit(ret)}
req_operation (Messages m_r, short ret) = {ConPurse sender.req_operation(m_r)exit(ret)}
ack_operation (Messages m_a, short ret) = {ConPurse sender.ack_operation(m_a)exit(ret)}
}


PROPERTY sender_purse {

STATES
{
STARTING { awaiting_from ; }
NORMAL { parties_initialised ;
         money_deducted ;
         end_transaction (init) ;
       }
}

TRANSITIONS
{
awaiting_from -> parties_initialised [m_sf.label == Messages.StartFrom && m_sf.id == sender.name && m_sf.cpd != null
                                      && m_sf.cpd.name != name & logIdx < exLog.length
                                      \ start_from_operation \ ret == SWResponses.SW_SUCCESS \ skip ;]

parties_initialised -> money_deducted [ m_r.label == Messages.Req && m_r.id == sender.name
                                        && checkSameTransaction(m_r) == SWResponses.SW_SUCCESS && transaction.value <= balance
                                        \ req_operation \ ret == SWResponses.SW_SUCCESS \ skip ; ]

money_deducted -> end_transaction [m_a.label == Messages.Ack && m_a.id == sender.name
                                  && checkSameTransaction(m_a) == SWResponses.SW_SUCCESS
                                  \ ack_operation \ ret == SWResponses.SW_SUCCESS \ skip ; ]
}
} %%end of PROPERTY

} %% end of MODEL


CINVARIANTS
{
ConPurse {(exLog != null) && (exLog.length > 0)}
ConPurse {(logIdx >= 0) && (logIdx <= exLog.length)}
ConPurse {(balance >= 0) && (balance <= ShortMaxValue)}
ConPurse {(nextSeq >= 0) && (nextSeq <= ShortMaxValue)}
ConPurse {(status >= 0) && (status <= 5)}
ConPurse {(transaction != null) && (transaction.value >= 0)}
ConPurse {(\forall int i; i>=0 && i<25; exLog[i] != null)}
ConPurse {name > 0}
}

INITPROP init { status == 4 }

